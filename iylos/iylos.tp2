// --------------------------------------------
// Mod setup
// --------------------------------------------
// Basics
BACKUP ~iylos/backup~
//AUTHOR ~Liam Esler (K'aeloree) - kae@spellholdstudios.net~
SUPPORT ~http://www.shsforums.net/forum/352-iylos/~
ASK_EVERY_COMPONENT
// Version
VERSION ~2.7~
// Readme
README ~iylos/iylos_readme.html~


ALWAYS

  ACTION_IF NOT VARIABLE_IS_SET bg2_chapter THEN BEGIN // check to make this happen only once per install

/* not really necessary because no chapter variable used but I'll put this here nevertheless */
    ACTION_IF GAME_IS ~eet~ BEGIN
      OUTER_SET bg2_chapter = 12
    END ELSE BEGIN
      OUTER_SET bg2_chapter = 0
    END
    OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
      OUTER_SET bg2_chapter = bg2_chapter + 1
      OUTER_SPRINT name_source ~bg2_chapter_%i%~
      OUTER_SET EVAL ~%name_source%~ = bg2_chapter
    END

    // convert strings to UTF-8 for BGEE/BG2EE
    ACTION_DEFINE_ARRAY lk#reload BEGIN setup END
    LAF HANDLE_CHARSETS 
	INT_VAR infer_charsets = 1 
	STR_VAR tra_path = ~iylos/translations~
	reload_array = lk#reload 
    END

  END

END

// Translations
AUTO_TRA ~iylos/translations/%s~

LANGUAGE ~English~
         ~english~
         ~iylos/translations/english/setup.tra~

// --------------------------------------------
// Iylos NPC Mod
// --------------------------------------------
// Component setup
BEGIN @1
  REQUIRE_PREDICATE GAME_IS ~tob bg2ee bgt eet~ @88

    
    // Adds custom IsValidForPartyDialogue state used throughout Iylos
    APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
      ~0x80101FEF CD_STATE_NOTVALID~
      UNLESS ~CD_STATE_NOTVALID~


    // Appending gtimes.ids to include the timer
    APPEND ~gtimes.ids~ ~3600 IYLOS_TIMER~
    UNLESS ~IYLOS_TIMER~


    // Compiling dialogue
    COMPILE ~iylos/dialogue/lk#iylo.d~
    COMPILE ~iylos/dialogue/blk#iyl.d~
    COMPILE ~iylos/dialogue/lk#iylj.d~
    COMPILE ~iylos/dialogue/lk#iylp.d~
    COMPILE ~iylos/dialogue/imoen25j.d~

    
    // Ascension crossmod dialogue

/* Ascension component "Ascension main component: rewritten Chapter 10" - DESIGNATED 0 */
    ACTION_IF MOD_IS_INSTALLED ~Ascension.tp2~ 0 BEGIN
      // Get state for IRENIC2 %iylos_irenic2% 
/* @456 = ~So my ungrateful sister, you finally stand by my side. I shall deal with your treachery afterwards. As for you, <CHARNAME>, Melissan has granted me the power to rip the divinity from your body. The first thing I shall feel in an eon shall be pleasure as I watch your corpse turn to dust.~ */
      OUTER_SET iylos_irenic2 = STATE_WHICH_SAYS 456 IN ~ascension/lang/%s/iren_bodhi.tra~ FROM ~IRENIC2~
      // Get state for BALTH2 %iylos_balth2_1% 
/* @1007 = ~She has, no doubt, guarded the pools in some fashion... but it is the only way to continue that I can think of. Short of standing here and watching her become a goddess, that is.~ */
      OUTER_SET iylos_balth2_1 = STATE_WHICH_SAYS 1007 IN ~ascension/lang/%s/pool_banter.tra~ FROM ~BALTH2~
      COMPILE EVALUATE_BUFFER ~iylos/dialogue/crossmod/ascension.d~
    END

/* Ascension component "Redeemable Balthazar" - DESIGNATED 10 */
    ACTION_IF MOD_IS_INSTALLED ~Ascension.tp2~ 10 BEGIN
      // Get state for BALTH2 %iylos_balth2_2% 
/* @1011 = ~And there is nothing that I ever wanted more. I choose to become a mortal gladly, and with all my heart. To walk the earth without that dark burden twisting my soul... I... I cannot even imagine...~ */
      OUTER_SET iylos_balth2_2 = STATE_WHICH_SAYS 1011 IN ~ascension/lang/%s/balth_finsol.tra~ FROM ~BALTH2~
      COMPILE EVALUATE_BUFFER ~iylos/dialogue/crossmod/ascension_10.d~
    END
    
    
    // Compiling other scripts
    COMPILE ~iylos/scripts/lk#icut.baf~
    COMPILE ~iylos/scripts/lk#iylo.baf~
      USING ~iylos/translations/%LANGUAGE%/lk#iylo_baf.tra~
    EXTEND_TOP ~ar6200.bcs~ ~iylos/scripts/ar6200.baf~
    EXTEND_BOTTOM ~ar5003.bcs~ ~iylos/scripts/ar5003.baf~
    EXTEND_BOTTOM ~imoe25.bcs~ ~iylos/scripts/imoe25.baf~


    // Copying items
    COPY ~iylos/items/lk#iamul.itm~ ~override~ // Amulet of Tranquillity
      SAY NAME1 @2
      SAY NAME2 @2
      SAY UNIDENTIFIED_DESC @3
      SAY DESC @4
    
    COPY ~iylos/items/lk#ibelt.itm~ ~override~ // Belt of Storage
      SAY NAME1 @5
      SAY NAME2 @5
      SAY UNIDENTIFIED_DESC @6
      SAY DESC @6
    
    COPY ~iylos/items/lk#ibelt.sto~ ~override~
    
    COPY ~iylos/items/lk#iboot.itm~ ~override~ // Boots of the Nomad
      SAY NAME1 @7
      SAY NAME2 @7
      SAY UNIDENTIFIED_DESC @8
      SAY DESC @9
    
    COPY ~iylos/items/lk#ibrac.itm~ ~override~ // Bracers of the Coiling Serpent
      SAY NAME1 @10
      SAY NAME2 @10
      SAY UNIDENTIFIED_DESC @11
      SAY DESC @12
    
    COPY ~iylos/items/lk#iclck.itm~ ~override~ // Cloak of the Deserts
      SAY NAME1 @13
      SAY NAME2 @13
      SAY UNIDENTIFIED_DESC @14
      SAY DESC @14
    
    COPY ~iylos/items/lk#igem1.itm~ ~override~ // Meditative Crystal
      SAY NAME1 @15
      SAY NAME2 @15
      SAY UNIDENTIFIED_DESC @16
      SAY DESC @17
    
    COPY ~iylos/items/lk#ihood.itm~ ~override~ // Aegis of the Deserts
      SAY NAME1 @18
      SAY NAME2 @18
      SAY UNIDENTIFIED_DESC @19
      SAY DESC @19
    
    COPY ~iylos/items/lk#ilet1.itm~ ~override~ // Sealed Letter
      SAY NAME1 @20
      SAY NAME2 @20
      SAY UNIDENTIFIED_DESC @21
      SAY DESC @21
    
    COPY ~iylos/items/lk#ilet2.itm~ ~override~ // Letter from Balthazar
      SAY NAME1 @22
      SAY NAME2 @22
      SAY UNIDENTIFIED_DESC @23
      SAY DESC @23
    
    COPY ~iylos/items/lk#iring.itm~ ~override~ // Sandstone Ring
      SAY NAME1 @24
      SAY NAME2 @24
      SAY UNIDENTIFIED_DESC @25
    
    COPY ~iylos/items/lk#irobe.itm~ ~override~ // Monastic Robe
      SAY NAME1 @26
      SAY NAME2 @26
      SAY UNIDENTIFIED_DESC @27
      SAY DESC @28
    
    
    //Copying images and creatures
    COPY ~iylos/images~ ~override~
    
    ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
      COPY ~iylos/portrait/ee~ ~override~
    END ELSE BEGIN
      COPY ~iylos/portrait/bg2~ ~override~
    END

    COPY ~iylos/creatures/lk#iylos.cre~ ~override~
      SAY NAME1 @29
      SAY NAME2 @29
      WRITE_EVALUATED_ASCII DIALOG          ~LK#IYLO~ #8
      WRITE_EVALUATED_ASCII DEATHVAR        ~iylos~ #32
      WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~LK#IYLO~ #8
      SAY BIO @30
      SAY MORALE @31
      SAY HAPPY @32
      SAY UNHAPPY_ANNOYED @33
      SAY UNHAPPY_SERIOUS @34
      SAY UNHAPPY_BREAKING @35
      SAY LEADER @36
      SAY TIRED @37
      SAY BORED @38
      SAY BATTLE_CRY1 @39
      SAY BATTLE_CRY2 @40
      SAY BATTLE_CRY3 @41
      SAY BATTLE_CRY4 @42
      SAY DAMAGE @43
      SAY DYING @44
      SAY HURT @45
      SAY AREA_FOREST @46
      SAY AREA_CITY @47
      SAY AREA_DUNGEON @48
      SAY AREA_DAY @49
      SAY AREA_NIGHT @50
      SAY SELECT_COMMON1 @51
      SAY SELECT_COMMON2 @52
      SAY SELECT_COMMON3 @53
      SAY SELECT_COMMON4 @54
      SAY SELECT_COMMON5 @55
      SAY SELECT_COMMON6 @56
      SAY SELECT_ACTION1 @57
      SAY SELECT_ACTION2 @58
      SAY SELECT_ACTION3 @59
      SAY SELECT_ACTION4 @60
      SAY SELECT_ACTION5 @61
      SAY SELECT_ACTION6 @62
      SAY SELECT_ACTION7 @63
      SAY SELECT_RARE1 @64
      SAY SELECT_RARE2 @65
      SAY CRITICAL_HIT @66
      SAY CRITICAL_MISS @67
      SAY TARGET_IMMUNE @68
      SAY INVENTORY_FULL @69
      SAY PICKED_POCKET @70
      SAY HIDDEN_IN_SHADOWS @71
      SAY SPELL_DISRUPTED @72
      SAY SET_A_TRAP @73
    
    
    // Appends
    APPEND ~pdialog.2da~  ~IYLOS LK#IYSP LK#IYSJ LK#IYSD LK#IYLP LK#IYLJ LK#IYTD LK#IYLO~
      UNLESS ~IYLOS~
    
    APPEND ~interdia.2da~ ~IYLOS BLK#ISO BLK#IYL~
      UNLESS ~IYLOS~
    

    // Epilogues
    COPY ~iylos/2da/lk#iylo1.2da~ ~override/lk#iylo1.2da~
      REPLACE ~99999~ @74

    COPY ~iylos/2da/lk#iylo1.2da~ ~override/lk#iylo2.2da~
      REPLACE ~99999~ @75

    COPY ~iylos/2da/lk#iylo1.2da~ ~override/lk#iylo3.2da~
      REPLACE ~99999~ @76
    
    
    // Audio
    COPY ~iylos/audio/lk#blank.mus~ ~music~
    
    COPY_EXISTING ~songlist.2da~ ~override~
      SET_2DA_ENTRY 0 2 3 ~lk#blank.mus~
    
    LAF HANDLE_AUDIO END
    


// --------------------------------------------
// Extended Content (PIDs & Friendship)
// --------------------------------------------
BEGIN @77
  REQUIRE_COMPONENT ~iylos.tp2~             ~0~ @78
    // Override script
    COMPILE ~iylos/extended/lk#iylo.baf~
      USING ~iylos/translations/%LANGUAGE%/lk#iylo_baf.tra~
    COMPILE ~iylos/extended/lk#iylj_extended.d~
//      USING ~iylos/translations/%LANGUAGE%/lk#iylj_extended.tra~

// --------------------------------------------
// Iylos' Talk Timers
// --------------------------------------------
// 15 minutes
BEGIN @79
  REQUIRE_COMPONENT ~iylos.tp2~             ~0~ @80
  SUBCOMPONENT @81
    COPY_EXISTING ~gtimes.ids~ ~override/gtimes.ids~
//      REPLACE_TEXTUALLY ~3600 IYLOS_TIMER~ ~900 IYLOS_TIMER~
    REPLACE_TEXTUALLY ~^.+[ %TAB%]\(IYLOS_TIMER[ %TAB%%LNL%%MNL%%WNL%]+\)~ ~900 \1~
      BUT_ONLY

// 20 minutes
BEGIN @82
  REQUIRE_COMPONENT ~iylos.tp2~             ~0~ @80
  SUBCOMPONENT @81
    COPY_EXISTING ~gtimes.ids~ ~override/gtimes.ids~
//      REPLACE_TEXTUALLY ~3600 IYLOS_TIMER~ ~1200 IYLOS_TIMER~
    REPLACE_TEXTUALLY ~^.+[ %TAB%]\(IYLOS_TIMER[ %TAB%%LNL%%MNL%%WNL%]+\)~ ~1200 \1~
      BUT_ONLY

// 30 minutes
BEGIN @83
  REQUIRE_COMPONENT ~iylos.tp2~             ~0~ @80
  SUBCOMPONENT @81
    COPY_EXISTING ~gtimes.ids~ ~override/gtimes.ids~
//      REPLACE_TEXTUALLY ~3600 IYLOS_TIMER~ ~1800 IYLOS_TIMER~
    REPLACE_TEXTUALLY ~^.+[ %TAB%]\(IYLOS_TIMER[ %TAB%%LNL%%MNL%%WNL%]+\)~ ~1800 \1~
      BUT_ONLY

// 45 minutes
BEGIN @84
  REQUIRE_COMPONENT ~iylos.tp2~             ~0~ @80
  SUBCOMPONENT @81
    COPY_EXISTING ~gtimes.ids~ ~override/gtimes.ids~
//      REPLACE_TEXTUALLY ~3600 IYLOS_TIMER~ ~2700 IYLOS_TIMER~
    REPLACE_TEXTUALLY ~^.+[ %TAB%]\(IYLOS_TIMER[ %TAB%%LNL%%MNL%%WNL%]+\)~ ~2700 \1~
      BUT_ONLY

// 60 minutes
BEGIN @85
  REQUIRE_COMPONENT ~iylos.tp2~             ~0~ @80
  SUBCOMPONENT @81
    COPY_EXISTING ~gtimes.ids~ ~override/gtimes.ids~
//      REPLACE_TEXTUALLY ~3600 IYLOS_TIMER~ ~3600 IYLOS_TIMER~
    REPLACE_TEXTUALLY ~^.+[ %TAB%]\(IYLOS_TIMER[ %TAB%%LNL%%MNL%%WNL%]+\)~ ~3600 \1~
      BUT_ONLY

// --------------------------------------------
// Alternate Iylos portrait by Ilmatar
// --------------------------------------------
// Component setup
BEGIN @86
  REQUIRE_COMPONENT ~iylos.tp2~             ~0~ @87

  ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
    COPY ~iylos/alternate/ee/lk#ilml.bmp~ ~override/lk#iyll.bmp~
    COPY ~iylos/alternate/ee/lk#ilmm.bmp~ ~override/lk#iylm.bmp~
    COPY ~iylos/alternate/ee/lk#ilms.bmp~ ~override/lk#iyls.bmp~
  END ELSE BEGIN
    COPY ~iylos/alternate/bg2/lk#ilml.bmp~ ~override/lk#iyll.bmp~
    COPY ~iylos/alternate/bg2/lk#ilmm.bmp~ ~override/lk#iylm.bmp~
    COPY ~iylos/alternate/bg2/lk#ilms.bmp~ ~override/lk#iyls.bmp~
  END